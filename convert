#!/usr/bin/gawk -f

function cmp_str_val(i1, v1, i2, v2) {
        # string value comparison, ascending order
        if (i1 ~ "sc") return -1
        if (i1 ~ "pso") return 1
        if (i2 ~ "sc") return 1
        if (i2 ~ "pso") return -1

        if (i1 ~ "cbmc") return -1
        if (i1 ~ "dpor") return 1
        if (i2 ~ "cbmc") return 1
        if (i2 ~ "dpor") return -1

        if (i1 < i2) return -1
        return (i1 != i2)
}

BEGIN {FS = ","}

{
        result[$1][$2][$3][$4][$5]["v"] = $6;
        result[$1][$2][$3][$4][$5]["r"] = ($7 == "crashed")?"!":(($7 == "incorrect")?"*":" ")
}

END {
        tools["cbmc"]
        tools["goto"]
        tools["dporc"]

        all=1

        PROCINFO["sorted_in"] = "cmp_str_val"

        for (bench in result) {
                for (bound in result[bench]) {
                        for (fence in result[bench][bound]) {
                                printf "%s & %s & %s ", bench, fence, bound
                                for (model in result[bench][bound][fence]) {
                                        min=600
                                        for (tool in tools)
                                                if (tool in result[bench][bound][fence][model]) {
                                                        v=result[bench][bound][fence][model][tool]["v"]
                                                        min=min<v?min:v
                                                }
                                                else all=0
                                        for (tool in tools)
                                                if (tool in result[bench][bound][fence][model]) {
                                                        v=result[bench][bound][fence][model][tool]["v"]
                                                        r=result[bench][bound][fence][model][tool]["r"]
                                                        form=(v==min)?"\\bf{%s %s}":"%s %s"
                                                        if (all == 1) {
                                                                tr=result[bench][bound][fence][model]["cbmc"]["r"]
                                                                form=(tr==r)?form:"{\\color{red}"form"}"
                                                        }
                                                        printf " & "form, r, v
                                                }
                                                else printf " & "
                                }
                                print " \\\\"
                        }
                        print "\\hline"
                }
                print "\\hline"
        }
}
